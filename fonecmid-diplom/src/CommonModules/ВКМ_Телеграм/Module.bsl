
#Область СлужебныеПроцедурыИФункции
 
Процедура ОтправитьСообщение() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ТокенУправленияТелеграмБотом.Значение КАК TokenTG,
		|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.Текст,
		|	ВКМ_ИдентификаторГруппыДляОповещения.Значение КАК Chat_Id
		|ИЗ
		|	Константа.ВКМ_ТокенУправленияТелеграмБотом КАК ВКМ_ТокенУправленияТелеграмБотом,
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту,
		|	Константа.ВКМ_ИдентификаторГруппыДляОповещения КАК ВКМ_ИдентификаторГруппыДляОповещения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		TokenTG = ВыборкаДетальныеЗаписи.TokenTG;
		Chat_Id = ВыборкаДетальныеЗаписи.Chat_Id;
		ТекстСообщения = ВыборкаДетальныеЗаписи.Текст;
		
		АдресТГ = "api.telegram.org";
		Соединение = Новый HTTPСоединение(АдресТГ, 443,,,,,Новый ЗащищенноеСоединениеOpenSSL());
		
		Данные = Новый Структура;
		Данные.Вставить("text", ТекстСообщения);
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, Данные);
		СтрокаJSON = ЗаписьJSON.Закрыть();
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		АдресЗапроса = "bot" + TokenTG + "/sendMessage?chat_id=" + Формат(Chat_Id, "ЧГ=");
		
		Запрос = Новый HTTPЗапрос(АдресЗапроса, Заголовки);
		Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
		
		Попытка
			Результат = Соединение.ОтправитьДляОбработки(Запрос);
			
			Если Не Результат.КодСостояния = 200 Тогда
				ВызватьИсключение "Ошибка проверки связи";
			КонецЕсли;
			
			Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			Объект.Удалить();
			
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;		
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти